#!/usr/bin/perl
#	vsftpd用のftpwhoコマンド
#	-count(-cに省略可)オプションを付けるとユーザー数だけ表示します。(ftpshow代替)
#	監視はwatchコマンドと組み合わせてください。(ftptop代替)
#   詳しい接続情報等も見たい場合はvsftpd.confで「setproctitle_enable=YES」にしてください。
use strict;
use warnings;
use Getopt::Long;

# psコマンドで表示されるvsftpdのプロセス
#my $VSFTPD = '/usr/sbin/vsftpd';   # setproctitle_enable=NOの場合
my $VSFTPD = 'vsftpd: ';    # setproctitle_enable=YESの場合

# 引数解析
my %opts = ('count' => 0);
GetOptions(\%opts, 'count');

# プロセス一覧取得
my @procs = `ps -eo user,pid,ppid,pcpu,pmem,vsz,rss,start_time,command`;
my $list = '';
my $count = 0;
my $rootpid = '';
foreach my $ps (grep(/$VSFTPD/, @procs)) {
	my($user, $pid, $ppid) = ($ps =~ /^(\w+)\s+(\d+)\s+(\d+)/);
	# 親プロセスがinit(1)ではなく、実効ユーザーがnobodyでないプロセスが対象
	if ($ppid == 1) {
		$rootpid = $pid;
	} elsif ($user ne 'nobody') {
		$count++;
		$list .= $ps if (!$opts{'count'});
	}
}

# 結果表示
print "master vsftpd pid:$rootpid\n";
print "$count users\n";
if ($list ne '') {
	print "\n";
	print $procs[0];
	print $list;
}
exit;
